/*
 * Code for the Arduino Nano to be used with the Go-Bot cart. The Nano will be contained in the front controller box, which will contain -  
 * 
 * Arduino Nano mounted to a PCB.
 * An analog joystick.
 * A joystick jack to allow for another joystick to be used instead of the analog joystick.
 * A switch to control the raising and lowering of the seat
 * A potentiometer to control the overall speed of the cart
 * Digital jacks for buttons/switches to control // Readings for Quick Stop, Brake Release, Button to shut off system, Seat-up command, and Seat-down command.
 * CAT5 cable to send the data to the back of the cart to the Arduino Mega.
 * 
 * This code will collect all data attached to the Arduino Nano and send it via the CAT5 cable back to the Arduino Mega.
 * 
 * By: Andrew Gates, Reagan Stovall and Jesse Wiklanski in conjunction with Robert Gutmann.
 * For: Mary Bridge Children's Therapy Unit at Good Samaritan Hospital.
 */
 
// Constanst for Analog Pins
const int potPin = A0;
const int yPin = A2;
const int xPin = A1;
 
// Constants for the digital Pins
const int forwardPin = 10;    // Digital pin for left signal
const int reversePin = 9;    // Digital right pin
const int leftPin = 8;       // Digital forward pin
const int rightPin = 7;      // Digital reverse pin
const int QSPin = 6;         // Digital Brake Release pin
const int BRPin = 5;         // Digital quick Stop pin
const int button = 4;        // Digital button from atari
const int SUPin = 3;         // Digital seat up pin
const int SDPin = 2;         // Digital seat down pin
const int onOffPin = 11;        // Button to turn system off
const int ledPin = 12;       // LED pin
int count = 0;
 
void setup() 
{
  pinMode(forwardPin, INPUT_PULLUP);  //2
  pinMode(reversePin, INPUT_PULLUP);  //3
  pinMode(leftPin, INPUT_PULLUP);     //4
  pinMode(rightPin, INPUT_PULLUP);    //5
  pinMode(QSPin, INPUT_PULLUP);       //6
  pinMode(BRPin, INPUT_PULLUP);       //7
  pinMode(button, INPUT_PULLUP);      //8
  pinMode(SUPin, INPUT_PULLUP);       //9
  pinMode(SDPin, INPUT_PULLUP);       //10
  pinMode(onOffPin, INPUT_PULLUP);       //11
  pinMode(ledPin, OUTPUT);            //12

 
  digitalWrite(ledPin, HIGH);         // Lets the user know the cart is on.
  Serial.begin(19200);
}
 
void loop() {
  // This sets the high reference for the analog read to whatever
  // voltage the AREE pin is reading, in this case, it should be
  // wired to the same power supply as the analog sensors... 3.3V
  analogReference(EXTERNAL);
 
  char start  = ':';   // starts the set
  char next   = ',';   // useded to break up the string
  int tempInt = 0;     // used as a dummy var
 
  // This bit filters any noise out of the center range first so
  // when idleing, there is a consistent zero read. Then the
  // temp Int is mapped to the range expected by the motor.
  // While the Sabertooth Motor COntroller needs a range from -2047
  // to 2047, the mapping is changed speciffically for this joystick
  // If any problems result, try reading the serial port of the Nano
  // and adjusting the range appropriately. keep in mind that positive
  // and negative shoud increase or deascrease the same amount.
  tempInt = analogRead(xPin);
  tempInt = (tempInt > 534 || tempInt < 490) ? tempInt : 512;
  tempInt = (map(tempInt, 0, 1024, -2407, 2407));
  if (tempInt <= -2047) tempInt = -2047;
  else if (tempInt >= 2047) tempInt = 2047;
  String xVal = String(tempInt);
  
  tempInt = analogRead(yPin);
  tempInt = (tempInt > 534 || tempInt < 490) ? tempInt : 512;
  tempInt = (map(tempInt, 0, 1024, -2447, 2447));
  if (tempInt <= -2047) tempInt = -2047;
  else if (tempInt >= 2047) tempInt = 2047;
  String yVal = String(tempInt);
  
  tempInt = analogRead(potPin);
  String pot = String(map(tempInt,  0, 1024, 20, 0));
 
  // These mappings are for the motor controller, order is important.
  tempInt = digitalRead(rightPin);
  String rightVal = String(map(tempInt, 0, 1, 2047, 0));
  tempInt = digitalRead(leftPin);
  String leftVal = String(map(tempInt, 0, 1, -2047, 0));
  tempInt = digitalRead(reversePin);
  String reverseVal = String(map(tempInt, 0, 1, -2047, 0));
  tempInt = digitalRead(forwardPin);
  String forwardVal = String(map(tempInt, 0, 1, 2047, 0));
 
  // This is a simple counter indicator to let the Mega know the data is still being sent
  count += 1;
  count = count % 10;
  
 
  // Readings for Quick Stop, Brake Release, Button, Seat-up command, and Seat-down command.
  String QSVal = String(digitalRead(QSPin), DEC);
  String BRVal = String(digitalRead(BRPin), DEC);
  String Btn   = String(digitalRead(button), DEC);
  String SUVal = String(digitalRead(SUPin), DEC);
  String SDVal = String(digitalRead(SDPin), DEC);
  String onOff = String(digitalRead(onOffPin), DEC);
  String Count = String(count, DEC);
 
  //array[0] = analogx
  //array[1] = analogy
  //array[2] = pot
  //array[3] = d-forward
  //array[4] = d-reverse
  //array[5] = d-left
  //array[6] = d-right
  //array[7] = d-quickstop
  //array[8] = d-brakerelease
  //array[9] = d-ataribtn
  //array[10] = d-seatup
  //array[11] = d-seatdown
  //array[12] = d-off
  //array[13] = count 
  // String to be sent over to the Mega containing all of the data.
  String sendString = String(start + xVal + next + yVal + next + pot + next + forwardVal
                             + next + reverseVal + next + leftVal + next + rightVal + next
                             + QSVal + next + BRVal + next + Btn + next + SUVal + next 
                             + SDVal + next + onOff + next + Count);
  Serial.println(sendString);
  
  Serial.flush();   // insures that all data in the buffer is sent before more
}
 
  
